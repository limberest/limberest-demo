plugins {
    id 'java'
    id 'war'
    id 'eclipse-wtp'
}

eclipse {
    classpath {
        defaultOutputDir = file("build/classes")
    }
    wtp {
        component {
            resource sourcePath: "/web", deployPath: "/"
            resource sourcePath: "/config", deployPath: "/WEB-INF/classes"
        }
        facet {
            facet type: "fixed", name: "jst.web"
            file.whenMerged {
                facets.find { it.type.name() == "installed" && it.name == "jst.web" }.version = "3.1"
            }
        }
    }
}

jar {
    baseName = 'limberest-demo'
    version =  '1.2.1'
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        java {
            srcDirs = ['src', 'test']
        }
        output.classesDir = 'build/classes'
    }
}

dependencies {
    
    compile 'io.limberest:limberest:1.2.1'
    compile 'ch.qos.logback:logback-core:1.1.9'
    compile 'ch.qos.logback:logback-classic:1.1.9'
    compile 'io.swagger:swagger-annotations:1.5.13'
    testCompile 'junit:junit:4.12'
}

task tests(type: Exec) {
    executable ((System.properties['os.name'].startsWith('Windows')) ? 'npm.cmd' : 'npm')
    args 'version'
}

war {
    // include configs on classpath
    into('WEB-INF/classes') {
        from('config') {
            include '*'
        }
    }
    archiveName = 'demo.war'
    webXml = file('web/WEB-INF/web.xml')
}
